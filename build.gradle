/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2014 Lee Collins
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'

mainClassName = 'io.github.javaconductor.gserv.Main'
group = "io.github.javaconductor"
version = "0.9.0"

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest.attributes("Main-Class": "io.github.javaconductor.gserv.Main")
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/groovy')
        resources.srcDir file('src/integrationTest/resources')
    }
    test {
        java.srcDir file('src/integrationTest/groovy')
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

repositories {
    mavenCentral()
    mavenLocal()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'log4j:log4j:1.2.17'
//    compile('org.codehaus.groovy:groovy-all:2.3.0')
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile('org.codehaus.gpars:gpars:1.1.0')
    compile('commons-io:commons-io:2.4')
    compile 'org.pegdown:pegdown:1.2.1'
    compile 'commons-cli:commons-cli:1.2'

    /// needed for Grab/Grape
    compile 'org.apache.ivy:ivy:2.2.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.objenesis:objenesis:1.2'
    testCompile "junit:junit:4.11"

    integrationTestCompile sourceSets.main.output
    integrationTestCompile configurations.testCompile
    integrationTestCompile sourceSets.test.output
    integrationTestRuntime configurations.testRuntime

}

task integration(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

groovydoc {

}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'gServ'
                packaging 'jar'
                // optionally artifactId can be defined here
                description "Framework for REST based services and SPAs in Groovy"

                url 'http://github.com/javaconductor/gservExamples'

                scm {
                    connection 'scm:git:git@github.com:javaconductor/gserv.git'
                    developerConnection 'scm:git:git@github.com:javaconductor/gserv.git'
                    url 'git@github.com:javaconductor/gserv.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'javaConductor'
                        name 'Lee Collins'
                        email 'javaConductor@yahoo.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

task standaloneJar(type: Jar) {
    classifier = 'jar'
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest.attributes("Main-Class": "io.github.javaconductor.gserv.Main")
    archiveName = "gserv-${version}.jar"
}

task frameworkJar(type: Jar) {
    classifier = 'jar'
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    archiveName = "gservFramework-${version}.jar"
    exclude('groovy/*')
    exclude('org/codehaus/groovy/*')
}

artifacts {
    archives standaloneJar
    archives frameworkJar
    archives sourcesJar
    archives groovydocJar
}
